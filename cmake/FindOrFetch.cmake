include(FetchContent)

function(FetchContent_CMakeProject NAME)
    FetchContent_Declare(${NAME} ${ARGN})
    FetchContent_GetProperties(${NAME})
    string(TOLOWER "${NAME}" name)
    if(NOT ${name}_POPULATED)
        message(STATUS "Fetching ${NAME}")
        FetchContent_Populate(${NAME})
        add_subdirectory(${${name}_SOURCE_DIR} ${${name}_BINARY_DIR} EXCLUDE_FROM_ALL)
    endif()
endfunction()

function(FindPackage_or_FetchContent NAME)
    set(options CONFIG)
    set(oneValueArgs CACHE_VAR)
    set(multiValueArgs )
    cmake_parse_arguments(FOF "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})
    if(FOF_CONFIG)
        set(_config CONFIG)
    endif()
    if(${FOF_CACHE_VAR})
        find_package(${NAME} REQUIRED ${_config})
    else()
        unset(${NAME}_DIR CACHE)
        unset(${NAME}_FOUND CACHE)
        FetchContent_CMakeProject(${NAME} ${FOF_UNPARSED_ARGUMENTS})
    endif()
endfunction()
